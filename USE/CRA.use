model CRA

class Detector
    attributes
        bienAnclado : Boolean init = true
        esperando : Boolean init = false
    operations
        perderAnclaje()
            begin
            self.bienAnclado := false;
            self.unidad.comunicarPerdida();
            end 
            pre: self.bienAnclado = true 
            post: self.bienAnclado = false
        unidadArreglada()
            begin 
            self.bienAnclado := true;
            if self.esperando = true then self.esperando := false end;
            self.unidad.reanudar();
            end  
            pre: self.bienAnclado = false 
            post: self.bienAnclado = true and self.esperando = false
    statemachines
        psm Anclaje
            states
            s : initial 
            BienAnclada 
            MalAnclada 
            transitions
            s -> BienAnclada 
            BienAnclada -> MalAnclada {[self.esperando = false] perderAnclaje() [self.bienAnclado = false]}
            MalAnclada -> BienAnclada {unidadArreglada() [self.bienAnclado = true]}
            end 
end

abstract class Unidad 
    attributes  
        numero : Integer
    operations 
        comunicarPerdida()
            begin 
            end
        espera()
            begin 
            end
        reanudar()
            begin 
            end

end 
 
class Vehiculo < Unidad 
    operations 
        comunicarPerdida()
            begin   
            if self.noria.operativo = true then self.noria.parada() end;
            self.cra.peticion(self, self.noria);
            end
        espera()
            begin 
            self.detector.esperando := true;
            end
            pre: self.detector.esperando = false
            post: self.detector.esperando = true
        reanudar()
            begin 
            self.noria.estaOperativo();
            end
end

class Vagon < Unidad
    operations 
        comunicarPerdida()
            begin   
            if self.montaniaRusa.operativo = true then self.montaniaRusa.parada() end;
            self.cra.peticion(self, self.montaniaRusa);
            end
        espera()
            begin 
            self.detector.esperando := true;
            end
            pre: self.detector.esperando = false
            post: self.detector.esperando = true
        reanudar()
            begin 
            self.montaniaRusa.estaOperativo();
            end
end

abstract class Atraccion
    attributes
        calle : String
        operativo : Boolean init = true
    operations
        parada()
            begin   
            end
        estaOperativo()
            begin 
            end 
        reanudar()
            begin
            end
    statemachines
        psm Estado
            states 
            s : initial 
            Operativa 
            Averiada 
            transitions
            s -> Operativa 
            Operativa -> Averiada {[self.operativo = true] parada() [self.operativo = false]}
            Averiada -> Operativa {[self.operativo = false] reanudar() [self.operativo = true]} 
            end
end

class Noria < Atraccion
    operations
        parada()
            begin   
            self.operativo := false
            end
            post: self.operativo = false
        estaOperativo() 
            begin
            if self.vehiculos.detector->forAll(x | x.bienAnclado = true or x = null) then self.reanudar() end
            end  
        reanudar()
            begin 
            self.operativo := true;
            end 
            pre: self.operativo = false
            post: self.operativo = true
end

class MontaniaRusa < Atraccion
    operations
        parada()
            begin   
            self.operativo := false
            end
            post: self.operativo = false
        estaOperativo() 
            begin
            if self.vagones.detector->forAll(x | x.bienAnclado = true or x = null) then self.reanudar() end
            end  
        reanudar() 
            begin 
            self.operativo := true;
            end 
            pre: self.operativo = false
            post: self.operativo = true
end

class CRA
    operations
        peticion(u : Unidad, a : Atraccion)
            begin 
            if self.dispositivos->forAll(x | x.operativo = false) then u.espera() else self.dispositivos->any(x | x.operativo = true).supervision(a.calle, u.numero) end
            end
end

class Dispositivo
    attributes
        avisosResueltos : Integer init = 0
        operativo : Boolean init = true
    operations
        supervision(c : String, n : Integer)
            begin
            self.operario.comenzarSupervision(c, n);
            end
        averiaSolucionada(c : String, n : Integer)
            begin
            self.avisosResueltos := self.avisosResueltos + 1;
            self.operativo := true;
            (self.cra.unidades->select(x | x.numero = n)->asSequence()->first()).detector.unidadArreglada();
            if self.cra.unidades.detector->select(x | x.esperando = true)->size() > 0 then self.cra.unidades->select(x | x.detector.esperando = true)->asSequence()->first().comunicarPerdida() end;
            end
            pre: self.operativo = false
end

class Operario
    attributes
        calleAtraccionAtendida : String
        numeroUnidadAtendida : Integer 
    operations
        comenzarSupervision(c : String, n : Integer) 
            begin 
            self.dispositivo.operativo := false;
            self.calleAtraccionAtendida := c;
            self.numeroUnidadAtendida := n;
            end 
            pre: self.dispositivo.operativo = true
            post: self.dispositivo.operativo = false and self.calleAtraccionAtendida <> null and self.numeroUnidadAtendida <> null 
        terminarSupervision()
            begin
            self.calleAtraccionAtendida := null;
            self.numeroUnidadAtendida := null;
            self.dispositivo.averiaSolucionada(self.calleAtraccionAtendida, self.numeroUnidadAtendida);
            end 
            pre: self.dispositivo.operativo = false 
            post:  self.calleAtraccionAtendida = null and self.numeroUnidadAtendida = null;
    statemachines
        psm Trabajo
            states
            s : initial 
            Trabajando 
            Libre 
            transitions
            s -> Libre 
            Trabajando -> Libre {terminarSupervision()}
            Libre -> Trabajando {comenzarSupervision() [self.dispositivo.operativo = false]} 
            end 
end

association Deteccion between 
    Unidad [1] role unidad
    Detector [0..1] role detector 
end 

association Posesion between
    Dispositivo [*] role dispositivos
    CRA [1] role cra 
end 

association Uso between 
    Operario [1] role operario 
    Dispositivo [1] role dispositivo 
end 

association Notificacion between 
    CRA [1] role cra 
    Unidad [*] role unidades
end 

aggregation VehiculoNoria between 
    Noria [1] role noria
    Vehiculo [*] role vehiculos 
end 

aggregation VagonMontaniaRusa between 
    MontaniaRusa [1] role montaniaRusa
    Vagon [*] role vagones
end 

constraints 

-- R1. Cada una de las unidades del parque tendrá un identificador único.
context Unidad inv IdentificadorUnico:
    Unidad.allInstances()->forAll(x, y | x <> y implies x.numero <> y.numero)

-- R2. Los identificadores de las unidades no pueden ser nulos.
context Unidad inv IdentificadorNoNulo:
    self.numero <> null 

-- R3. Todos los vehículos de una noria tendrán detector.
context Noria inv VehiculosDetectores:
    self.vehiculos.detector <> null

-- R4. Todos los vagones de una montaña rusa tendrán detector menos el delantero.
context MontaniaRusa inv VagonesDetectores:
    (self.vagones->size() = (self.vagones->select(x | x.detector <> null))->size() + 1) or self.vagones->size() = 0

-- R5. Toda atracción debe tener una calle.
context Atraccion inv NombreCalle:
    self.calle <> null